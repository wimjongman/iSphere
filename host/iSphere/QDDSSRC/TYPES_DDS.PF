      *------------------------------------------------------------------------*
      *                                                                        *
      * Copyright (c) 2012-2017 iSphere Project Owners                         *
      * All rights reserved. This program and the accompanying materials       *
      * are made available under the terms of the Common Public License v1.0   *
      * which accompanies this distribution, and is available at               *
      * http://www.eclipse.org/legal/cpl-v10.html                              *
      *                                                                        *
      *------------------------------------------------------------------------*
      *                                                                        *
      * Test table with various SQL data types                                 *
      *                                                                        *
      *------------------------------------------------------------------------*
      *   >>PRE-COMPILER<<                                                     *
      *     >>CRTCMD<<  CRTPF         FILE(&LI/&OB) +                          *
      *                               SRCFILE(&SL/&SF) SRCMBR(&SM);            *
      *     >>COMPILE<<                                                        *
      *       >>PARM<<                MAXMBRS(1);                              *
      *       >>PARM<<                MBR(*FILE);                              *
      *       >>PARM<<                FILETYPE(*DATA);                         *
      *       >>PARM<<                OPTION(*EVENTF);                         *
      *     >>END-COMPILE<<                                                    *
      *     >>EXECUTE<<                                                        *
      *     >>CMD<<     STRJRNPF FILE(&LI/&OB) +                               *
      *                          JRN(&LI/JRN) IMAGES(*BOTH) +                  *
      *                          OMTJRNE(*OPNCLO);                             *
      *   >>END-PRE-COMPILER<<                                                 *
      *------------------------------------------------------------------------*
     A                                      UNIQUE
      *
     A          R FTYPES                    TEXT('Test table with various +
     A                                      DDS data types')
      *
     A            D_BIN1         1B 0       TEXT('Binary 1-bytes')
     A                                      DFT(1)
     A            D_BIN2         4B 0       TEXT('Binary 2-bytes')
     A                                      DFT(2)
     A            D_BIN4         9B 0       TEXT('Binary 4-bytes')
     A***                                   DFT(4)
     A            D_BIN8        18B 0       TEXT('Binary 8-bytes')
     A                                      DFT(8)
      *
     A            D_REAL         9F 4       TEXT('Float 4-bytes 9,4')
     A                                      FLTPCN(*SINGLE)
     A            D_DOUBLE      17F 4       TEXT('Float 8-bytes 17,4')
     A                                      FLTPCN(*DOUBLE)
      *
     A            D_ZONED       30S 5       TEXT('Zoned 30,5')
     A                                      DFT(666.66)
     A            D_PACKED      30P 5       TEXT('Packed 30,5')
     A                                      DFT(777.77)
      *
     A            D_CHAR273     20A         TEXT('Chr Ccsid 273 20-bytes')
     A                                      CCSID(273)
     A                                      DFT('<Char-273-öäüÖÄÜß-->')
     A            D_CHAR37      20A         TEXT('Char Ccsid 37 20-bytes')
     A                                      CCSID(37)
     A                                      DFT('<Char-37-öäüÖÄÜß--->')
     A            D_VARCHAR    100A         TEXT('Varchar 100-bytes')
     A                                      VARLEN(20)
     A                                      CCSID(273)
     A                                      DFT('<Varchar 100-bytes->')
     A            D_BINARY      20A         TEXT('Binary 20-bytes')
     A                                      CCSID(65535)
     A                                      DFT(X'89E29788859985404040+
     A                                      40404040404040404040')
     A            D_VARBIN     100A         TEXT('Varbin 100-bytes')
     A                                      VARLEN(20)
     A                                      CCSID(65535)
     A                                      DFT(X'89E29788859985404040+
     A                                      40404040404040404040')
      *
     A            D_DATE_ISO      L         TEXT('Date-ISO') DATFMT(*ISO)
     A***                                   DFT('2017-08-25')
     A            D_TIME_ISO      T         TEXT('Time-ISO') TIMFMT(*ISO)
     A***                                   DFT('20.15.17')
     A            D_TMSTMP        Z         TEXT('Timestamp')
     A                                      DFT('1970-01-01-23.59.59.999999')
     A            D_DATE_EUR      L         TEXT('Date-EUR') DATFMT(*EUR)
     A                                      DFT('01.01.1970')
     A            D_DATE_JUL      L         TEXT('Date-Julian') DATFMT(*JUL)
     A                                      DFT('70.001') DATSEP('.')
      *
     A            D_TIME_HMS      T         TEXT('Time-HMS') TIMFMT(*HMS)
     A                                      DFT('23:59:59')
     A            D_TIME_USA      T         TEXT('Time-USA') TIMFMT(*USA)
     A                                      DFT('11:59 pm')
      *
     A            D_GRAPHIC     20G         TEXT('Graphic 20-bytes')
     A                                      CCSID(1200)
     A                                      DFT('<Graphic 20-bytes-->')
     A            D_VARGRA      20G         TEXT('<Vargraphic 20byte>')
     A                                      VARLEN(20)
     A                                      CCSID(13488)
     A                                      DFT('+-VGRAPHIC 13488---+')
      *
     A            D_BOOL         1A         TEXT('Boolean')
     A                                      DFT('0')
     A                                      VALUES('1' '0')
      *
     A            D_NULLABLE    10A         TEXT('Char Null Allowed')
     A                                      CCSID(273)
     A                                      ALWNULL
     A                                      DFT(*NULL)
      *
     A          K D_BIN4
      *
