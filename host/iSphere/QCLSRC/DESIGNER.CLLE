/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2012-2014 iSphere Project Owners                             */
/* All rights reserved. This program and the accompanying materials           */
/* are made available under the terms of the Common Public License v1.0       */
/* which accompanies this distribution, and is available at                   */
/* http://www.eclipse.org/legal/cpl-v10.html                                  */
/*                                                                            */
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* 5250 Emulator - Designer                                                   */
/*                                                                            */
/*----------------------------------------------------------------------------*/
/* STRPREPRC Compile Options:                                                 */
/*   >>PRE-COMPILER<<                                                         */
/*     >>CRTCMD<<    CRTBNDCL   PGM(&LI/&OB) +                                */
/*                              SRCFILE(&SL/&SF) SRCMBR(&SM);                 */
/*       >>COMPILE<<                                                          */
/*         >>PARM<<  DFTACTGRP(*NO);                                          */
/*         >>PARM<<  ACTGRP(ISPHERE);                                         */
/*         >>PARM<<  DBGVIEW(*NONE);                                          */
/*         >>PARM<<  OPTIMIZE(*FULL);                                         */
/*         >>PARM<<  OPTION(*EVENTF);                                         */
/*         >>PARM<<  TGTRLS(&TR);                                             */
/*       >>END-COMPILE<<                                                      */
/*       >>EXECUTE<<                                                          */
/*   >>END-PRE-COMPILER<<                                                     */
/*----------------------------------------------------------------------------*/

             PGM
     /* Declaration */
             DCLF       FILE(DESIGNER)
             DCL        VAR(&CMD) TYPE(*CHAR) LEN(3000)
             DCL        VAR(&LEN) TYPE(*DEC) LEN(15 5) VALUE(3000)
             DCL        VAR(&CNTN) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DLT) TYPE(*CHAR) LEN(10)
             DCL        VAR(&XLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&XSRCF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OPT) TYPE(*CHAR) LEN(1)
     /* Start run */
             CHGVAR     VAR(&IN50) VALUE('0')
     /* Get input */
 STEP010:    SNDRCVF    RCDFMT(HEADER)
             CHGVAR     VAR(&IN50) VALUE('1')
     /* Stop run */
             IF         COND(&IN03 *EQ '1') THEN(GOTO CMDLBL(STEP999))
     /* Initialize variable content */
             CHGVAR     VAR(&XLIB) VALUE(&LIB)
             CHGVAR     VAR(&XSRCF) VALUE(&SRCF)
             IF         COND(&MODE *EQ '*BROWSE' *AND (&EDITOR *EQ +
                          '*SDA' *OR &EDITOR *EQ '*RLU')) THEN(DO)
     /* Attention */
             SNDRCVF    RCDFMT(ATTENTION)
     /* Initialize "Continue" */
             CHGVAR     VAR(&CNTN) VALUE('*YES')
     /* Check, if source file does already exist */
             CHKOBJ     OBJ(QTEMP/QTN5250J) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
     /* Create source file */
             CRTSRCPF   FILE(QTEMP/QTN5250J) RCDLEN(92) MBR(*NONE) +
                          TEXT(*BLANK)
     /* Source file could`t be created */
             MONMSG     MSGID(CPF7302) EXEC(DO)
             CHGVAR     VAR(&CNTN) VALUE('*NO')
             ENDDO
             ENDDO
             IF         COND(&CNTN *EQ '*YES') THEN(DO)
     /* Check, if member does already exist */
             CHGVAR     VAR(&DLT) VALUE('*YES')
             CHKOBJ     OBJ(QTEMP/QTN5250J) OBJTYPE(*FILE) +
                          MBR(&MBR)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             CHGVAR     VAR(&DLT) VALUE('*NO')
             ENDDO
             IF         COND(&DLT *EQ '*YES') THEN(DO)
     /* Delete member */
             RMVM       FILE(QTEMP/QTN5250J) MBR(&MBR)
     /* Member couldn`t be deleted */
             MONMSG     MSGID(CPF7310) EXEC(DO)
             CHGVAR     VAR(&CNTN) VALUE('*NO')
             ENDDO
             ENDDO
             ENDDO
             IF         COND(&CNTN *EQ '*YES') THEN(DO)
     /* Copy member */
             CPYSRCF    FROMFILE(&XLIB/&XSRCF) +
                          TOFILE(QTEMP/QTN5250J) FROMMBR(&MBR) +
                          TOMBR(&MBR)
     /* Member couldn`t be copied */
             MONMSG     MSGID(CPF0000) EXEC(DO)
             CHGVAR     VAR(&CNTN) VALUE('*NO')
             ENDDO
             ENDDO
     /* Problems occured */
             IF         COND(&CNTN *EQ '*NO') THEN(DO)
             GOTO       CMDLBL(STEP010)
             ENDDO
     /* Change variable content */
             CHGVAR     VAR(&XLIB) VALUE('QTEMP')
             CHGVAR     VAR(&XSRCF) VALUE('QTN5250J')
             ENDDO
     /* Change library list */
             CHGVAR     VAR(&CMD) VALUE('CHGLIBL LIBL(' *TCAT &LIBL +
                          *TCAT ') CURLIB(' *TCAT &CURLIB *TCAT ')')
             CALL       PGM(QCMDEXC) PARM(&CMD &LEN)
             MONMSG     MSGID(CPF0000) EXEC(DO)
             GOTO       CMDLBL(STEP010)
             ENDDO
     /* Start SDA */
             IF         COND(&EDITOR *EQ '*SDA') THEN(DO)
             STRSDA     OPTION(1) SRCFILE(&XLIB/&XSRCF) SRCMBR(&MBR)
             MONMSG     MSGID(CPF0000)
             ENDDO
     /* Start RLU */
             IF         COND(&EDITOR *EQ '*RLU') THEN(DO)
             STRRLU     SRCFILE(&XLIB/&XSRCF) SRCMBR(&MBR) OPTION(2)
             MONMSG     MSGID(CPF0000)
             ENDDO
     /* Start SEU */
             IF         COND(&EDITOR *EQ '*SEU') THEN(DO)
             CHGVAR     VAR(&OPT) VALUE(' ')
             IF         COND(&MODE *EQ '*EDIT') THEN(DO)
             CHGVAR     VAR(&OPT) VALUE('2')
             ENDDO
             IF         COND(&MODE *EQ '*BROWSE') THEN(DO)
             CHGVAR     VAR(&OPT) VALUE('5')
             ENDDO
             IF         COND(&OPT *NE ' ') THEN(DO)
             STRSEU     SRCFILE(&XLIB/&XSRCF) SRCMBR(&MBR) OPTION(&OPT)
             MONMSG     MSGID(CPF0000)
             ENDDO
             ENDDO
     /* To the top */
             GOTO       CMDLBL(STEP010)
     /* Stop run */
 STEP999:    SIGNOFF
             ENDPGM
