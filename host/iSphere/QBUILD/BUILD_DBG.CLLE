     /*-----------------------------------------------------------------------*/
     /*                                                                       */
     /* Copyright (c) 2012-2014 iSphere Project Owners.                       */
     /* All rights reserved. This program and the accompanying materials      */
     /* are made available under the terms of the Common Public License v1.0  */
     /* which accompanies this distribution, and is available at              */
     /* http://www.eclipse.org/legal/cpl-v10.html                             */
     /*                                                                       */
     /* Contributors:                                                         */
     /*     Task Force IT-Consulting GmbH - initial API and implementation    */
     /*                                                                       */
     /*-----------------------------------------------------------------------*/
     /*                                                                       */
     /* Erstellen iSphere Bibliothek - Entfernen Debug                        */
     /*                                                                       */
     /*-----------------------------------------------------------------------*/
     /* STRPREPRC Compile Options:                                            */
     /*   >>PRE-COMPILER<<                                                    */
     /*     >>CRTCMD<<    CRTBNDCL   PGM(&LI/&OB) +                           */
     /*                              SRCFILE(&SL/&SF) SRCMBR(&SM);            */
     /*       >>COMPILE<<                                                     */
     /*         >>PARM<<   DFTACTGRP(*YES);                                   */
     /*       >>END-COMPILE<<                                                 */
     /*       >>EXECUTE<<                                                     */
     /*   >>END-PRE-COMPILER<<                                                */
     /*-----------------------------------------------------------------------*/
             PGM        PARM(&PRDLIB &TMPLIB)

             DCL        VAR(&PRDLIB  ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TMPLIB  ) TYPE(*CHAR) LEN(10)

             DCL        VAR(&TMPFILE ) TYPE(*CHAR) LEN(10) VALUE('BUILD_DBG')
             DCL        VAR(&NUM_RCDS) TYPE(*DEC)  LEN(10 0)

             DCLF       FILE(QADSPOBJ) OPNID(OBJ)

             OVRDBF     FILE(QADSPOBJ) TOFILE(&TMPLIB/BUILD_OWN) OVRSCOPE(*CALLLVL)

             DSPOBJD    OBJ(&PRDLIB/*ALL) OBJTYPE(*PGM *SRVPGM) +
                          DETAIL(*BASIC) OUTPUT(*OUTFILE) +
                          OUTFILE(&TMPLIB/&TMPFILE) OUTMBR(*FIRST +
                          *REPLACE)
             RTVMBRD    FILE(&TMPLIB/&TMPFILE) NBRCURRCD(&NUM_RCDS)

             OPNDBF     FILE(&TMPLIB/&TMPFILE) OPTION(*INP) +
                          OPNID(OBJ)

             IF         COND(&NUM_RCDS > 0) THEN(DO)
 LOOP:          RCVF       OPNID(OBJ)

                IF         COND(&OBJ_ODOBNM *EQ 'BUILD') THEN(DO)
                   GOTO       CMDLBL(LOOP)
                ENDDO

                IF         COND(&OBJ_ODOBTP *EQ '*PGM') THEN(DO)
                   CHGPGM     PGM(&OBJ_ODLBNM/&OBJ_ODOBNM) +
                                RMVOBS(*DBGDTA)
                ENDDO

                IF         COND(&OBJ_ODOBTP *EQ '*SRVPGM') THEN(DO)
                   CHGSRVPGM  SRVPGM(&OBJ_ODLBNM/&OBJ_ODOBNM) +
                                RMVOBS(*DBGDTA)
                ENDDO

                CHGVAR     VAR(&NUM_RCDS) VALUE(&NUM_RCDS - 1)
                IF         COND(&NUM_RCDS > 0) THEN(DO)
                   GOTO       CMDLBL(LOOP)
                ENDDO
             ENDDO

             CLOF       OPNID(OBJ)

             ENDPGM
